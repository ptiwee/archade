pkgname=libretro-arcade-git
pkgver=r178.6324165
pkgrel=1
pkgdesc="Libretro implementation for arcade games. (Final Burn Alpha)"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/libretro/fbalpha"
license=('custom:Non-Commercial')
groups=('libretro')
makedepends=('git')
_libname=fbalpha_libretro
_gitname=fbalpha
source=("git+https://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  RPI=`grep -m1 'Revision' /proc/cpuinfo | awk '{print $3}'`
  case "${RPI}" in
    a[0|2][1|2]04[0-2]) PLATFORM="platform=rpi2" ;;
    a[0|2]2082) PLATFORM="platform=rpi3" ;;
  esac

  cd "${_gitname}"
  make -f makefile.libretro ${PLATFORM}
}

package() {
  install -Dm644 "${_gitname}/${_libname}.so" "${pkgdir}/usr/lib/libretro/libretro-arcade.so"
  install -Dm644 "${_gitname}/src/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
