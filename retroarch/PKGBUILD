pkgname=retroarch
pkgver=1.3.6
pkgrel=1
pkgdesc="Libretro frontend"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/libretro/RetroArch"
license=('GPL3')
groups=('libretro')
depends=('sdl2')
makedepends=('git')
_gitname=RetroArch
source=("git+https://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

prepare() {
  cd "${_gitname}"
  git submodule init
  git submodule update
  git checkout v${pkgver}
}

build() {
  if [ "${CARCH}" == 'armv7h' ]; then
    NEON="--enable-neon";
  fi

  if [ "${CARCH}" == 'armv6h' -o "${CARCH}" == 'armv7h' ]; then
    ARM="--enable-dispmanx --enable-floathard";
  fi

  cd "${_gitname}"
  ./configure --prefix=/usr --disable-materialui --disable-xmb $NEON $ARM
  make
}

package() {
  cd "${_gitname}"
  make install DESTDIR="${pkgdir}"
}
