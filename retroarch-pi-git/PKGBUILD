pkgname=retroarch-pi-git
_gitname=RetroArch
epoch=2
pkgver=1.3.6.r547.339ea74
pkgrel=1
pkgdesc="Simple frontend for the Libretro API"
arch=('armv7h')
url="http://github.com/libretro/RetroArch"
license=('GPL3')
depends=('ffmpeg' 'python' 'sdl2')
makedepends=('git')
provides=('retroarch')
conflicts=('retroarch')
source=("git+https://github.com/libretro/$_gitname")
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"

  printf "%s" "$(git describe --first-parent --long --tags | sed 's/v//g;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$_gitname"

  git submodule init
  git submodule update
}

build() {
  cd "$_gitname"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname"

  make install DESTDIR="$pkgdir"
}
