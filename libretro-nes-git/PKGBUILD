pkgname=libretro-nes-git
pkgver=r678.b66580a
pkgrel=1
pkgdesc="Libretro implementation for NES games. (Nestopia)"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/libretro/nestopia"
license=('GPL-2.0')
groups=('libretro')
makedepends=('git')
_libname=nestopia_libretro
_gitname=nestopia
source=("git+https://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  RPI=`grep -m1 'Revision' /proc/cpuinfo | awk '{print $3}'`
  case "${RPI}" in
    a[0-2][1-2]04[0-2]) PLATFORM="platform=rpi2" ;;
    a[0-2]2082) PLATFORM="platform=rpi3" ;;
  esac

  cd "${_gitname}/libretro"
  make ${PLATFORM}
}

package() {
  install -Dm644 "${_gitname}/libretro/${_libname}.so" "${pkgdir}/usr/lib/libretro/libretro-nes.so"
}
