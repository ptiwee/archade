pkgname=libretro-arcade-pi-git
pkgver=144.25ed7a6
pkgrel=2
pkgdesc="Libretro implementation for arcade games (Final Burn Alpha)"
arch=('armv7h')
url="https://github.com/libretro/fbalpha"
license=('custom:Non-commercial')
makedepends=('git')

_libname=fbalpha_libretro
_gitname=fbalpha
source=("git+https://github.com/libretro/${_gitname}.git"
	"https://raw.github.com/libretro/libretro-super/master/dist/info/${_libname}.info")
sha256sums=('SKIP'
	'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  RPI=`grep -m1 'Revision' /proc/cpuinfo | awk '{print $3}'`
  case "${RPI}" in
    a[0-2]1041)	PLATFORM="platform=rpi2" ;;
    a[0-2]2082)	PLATFORM="platform=rpi3" ;;
  esac
  cd "${_gitname}"
  make -f makefile.libretro ${PLATFORM}
}

package()
{
  install -Dm644 "${_gitname}/${_libname}.so" "${pkgdir}/usr/lib/libretro/libretro-arcade.so"
  install -Dm644 "${_libname}.info" "${pkgdir}/usr/lib/libretro/libretro-arcade.info"
  install -Dm644 "${_gitname}/src/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
